
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
    id 'maven-publish'
    id 'java'
    id 'com.palantir.git-version' version '0.11.0'
}

group 'xyz.notarealtree.jzkill'
version gitVersion()
sourceCompatibility = 1.8
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}
shadowJar {
    baseName = 'jzkill'
    classifier = ''
    version = project.version
}

dependencies {
    compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.8.0-beta2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.1'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.9.5'
    compile 'org.immutables:value:2.5.6'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "notarealtree"
            name "Francis Screene"
            email "francis.screene@gmail.com"
        }
    }

    scm {
        url "https://github.com/NotARealTree/jzkill"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'xyz.notarealtree'
            artifactId 'jzkill'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'ZKill RedisQ parser')
                root.appendNode('name', 'JZKill')
                root.appendNode('url', 'https://github.com/NotARealTree/jzkill')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'jzkill'
        name = 'jzkill'
        userOrg = 'notarealtree'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/NotARealTree/jzkill'
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }

}
